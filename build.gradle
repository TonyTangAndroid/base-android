buildscript {
    apply from: 'constants.gradle'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath deps.agp
        classpath deps.kotlinGradlePlugin
        classpath deps.dokka
    }
}

allprojects { project ->
    group = groupId
    def repoUrl = "https://raw.github.com/arunkumar9t2/base-android/master/maven-repo/"

    repositories {
        google()
        jcenter()
        maven {
            url "${repoUrl}releases"
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroup group
            }
        }
        maven {
            url "${repoUrl}snapshots"
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroup group
            }
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

def sampleAppName = "sample-app"
def isSnapshot = project.hasProperty("snapshot")

subprojects { subProject ->
    apply from: '../constants.gradle'
    if (subProject.name != sampleAppName) {
        subProject.ext.artifact = subProject.name
        subProject.version = isSnapshot ? "master-SNAPSHOT" : subProject.ext.moduleVersion[subProject.name]
        apply from: '../libraryModule.gradle'
        apply from: '../publishing.gradle'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}