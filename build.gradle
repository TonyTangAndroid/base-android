buildscript {
    apply from: 'constants.gradle'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
    }
}

allprojects {
    group = 'dev.arunkumar.android'
    version = '1.0.0'
    def repoUrl = "https://raw.github.com/arunkumar9t2/base-android/master/maven-repo/"

    repositories {
        google()
        jcenter()
        maven {
            url "${repoUrl}releases"
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroup group
            }
        }
        maven {
            url "${repoUrl}snapshots"
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroup group
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

subprojects { project ->
    apply from: '../constants.gradle'
    if (project.name != "sample-app") {
        project.ext.artifact = project.name
        project.version = project.ext.moduleVersion[project.name]
        apply from: '../libraryModule.gradle'
        apply from: '../publishing.gradle'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
