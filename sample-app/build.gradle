apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion buildDetails.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "dev.arunkumar.android"
        minSdkVersion buildDetails.minSdk
        targetSdkVersion buildDetails.targetSdk

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "dep"

    productFlavors {
        local {
            dimension "dep"
        }
        remote {
            dimension "dep"
        }
        snapshot {
            dimension "dep"
        }
    }

    variantFilter { variant ->
        if (variant.name == "localRelease") {
            variant.setIgnore(true)
        }
        if (variant.name == "snapshotRelease") {
            variant.setIgnore(true)
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Common
    localImplementation project(path: ':common')
    remoteImplementation "dev.arunkumar.android:common:1.0.0"

    // Dagger related
    localImplementation project(path: ':dagger-base')
    remoteImplementation "dev.arunkumar.android:dagger-base:1.0.0"
    snapshotImplementation "dev.arunkumar.android:dagger-base:1.0-SNAPSHOT"
    localImplementation project(path: ':dagger-viewmodel')
    remoteImplementation "dev.arunkumar.android:dagger-viewmodel:1.0.0"

    // Logging
    localImplementation project(path: ':logging')
    remoteImplementation "dev.arunkumar.android:logging:1.0.0"

    // Viewmodel
    localImplementation project(path: ':viewmodel-utils')
    remoteImplementation "dev.arunkumar.android:viewmodel-utils:1.0.0"

    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"
    kaptTest "com.google.dagger:dagger-compiler:${versions.dagger}"
    kaptTest "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"
    implementation "io.reactivex.rxjava2:rxkotlin:${versions.rxKotlin}"
    implementation "com.jakewharton.rxbinding2:rxbinding:${versions.rxBinding}"

    // AndroidX
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support.constraint:constraint-layout:${versions.constraintLayout}"

    // Lifecycle extensions
    implementation "android.arch.lifecycle:extensions:${versions.lifecycleExtensions}"

    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.robolectric:robolectric:${versions.roboelectric}"
}
