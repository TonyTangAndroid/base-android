buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.realmPlugin
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion buildDetails.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "dev.arunkumar.android"
        minSdkVersion buildDetails.minSdk
        targetSdkVersion buildDetails.targetSdk

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "dep"

    productFlavors {
        local {
            dimension "dep"
        }
        remote {
            dimension "dep"
        }
        snapshot {
            dimension "dep"
        }
    }

    variantFilter { variant ->
        if (variant.name == "localRelease") {
            variant.setIgnore(true)
        }
        if (variant.name == "snapshotRelease") {
            variant.setIgnore(true)
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Common
    localImplementation project(path: ':common')
    remoteImplementation "dev.arunkumar.android:common:1.0.0"

    // Dagger related
    localImplementation project(path: ':dagger-base')
    remoteImplementation "dev.arunkumar.android:dagger-base:1.0.0"
    snapshotImplementation "dev.arunkumar.android:dagger-base:master-SNAPSHOT"
    localImplementation project(path: ':dagger-viewmodel')
    remoteImplementation "dev.arunkumar.android:dagger-viewmodel:1.0.0"
    snapshotImplementation "dev.arunkumar.android:dagger-viewmodel:master-SNAPSHOT"

    // Logging
    localImplementation project(path: ':logging')
    remoteImplementation "dev.arunkumar.android:logging:1.0.0"
    snapshotImplementation "dev.arunkumar.android:logging:master-SNAPSHOT"

    // Viewmodel
    localImplementation project(path: ':viewmodel-utils')
    remoteImplementation "dev.arunkumar.android:viewmodel-utils:1.0.0"
    snapshotImplementation "dev.arunkumar.android:viewmodel-utils:master-SNAPSHOT"

    // Rx utils
    localImplementation project(path: ':rx-utils')
    remoteImplementation "dev.arunkumar.android:rx-utils:1.0.0"
    snapshotImplementation "dev.arunkumar.android:rx-utils:master-SNAPSHOT"

    // Epoxy utils
    localImplementation project(path: ':epoxy-utils')
    remoteImplementation "dev.arunkumar.android:epoxy-utils:1.0.0"
    snapshotImplementation "dev.arunkumar.android:epoxy-utils:master-SNAPSHOT"

    implementation project(path: ':orbit')

    implementation deps.daggerAndroid
    implementation deps.daggerAndroidSupport
    kapt deps.daggerCompiler
    kapt deps.daggerAndroidProcessor
    kaptTest deps.daggerCompiler
    kaptTest deps.daggerAndroidProcessor

    // Kotlin
    implementation deps.kotlinStdLib

    // Rx
    implementation deps.rxAndroid
    implementation deps.rxKotlin
    implementation deps.rxBinding

    // Epoxy
    implementation deps.epoxyPaging
    kapt deps.epoxyProcessor

    // AndroidX
    implementation deps.androidXAppCompat
    implementation deps.constraintLayout

    // Lifecycle extensions
    implementation deps.androidXLifecycle
}
